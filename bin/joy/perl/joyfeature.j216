#
# joyfeature.216
#
# Optional structural features for joy 5.0
# (identical to joy 216)
#
# Type
# % mkjoyf joyfeature.default
#
# to generate tem_216.c and tem_216.h.
#
# 21 Apr 1999

#generic rules {
#if (ALI.sequence eq ' ' || ALI.sequence eq '-' || ALI.sequence eq '/') {
#	return '-';
#}
#}

feature "secondary structure and phi angle" {

/* adopted from joy216 code */

double phi216;
double psi216;
double calpsi;

phi216 = SST.phi;
psi216 = SST.psi;

if (SST.dssp eq 'E') {
	return 'E';
}
else if ((SST.dssp eq 'H') || (SST.dssp eq 'G') || (SST.dssp eq 'I')) { # all type of helices
	return 'H';
}
else if (phi216 > 180.0 || psi216 > 180.0) {
	return 'X';
}

/* phi216 >= 0 psi216 >0  */

if ((phi216 >= 0.0) && (psi216 > 0.0)) {
	calpsi = 155.0 - ((80.0 * phi216)/180.0);
	if (psi216 <= calpsi) {
		if (psi216 < 10.0) {
			if ((phi216 >= 80.0) && (phi216 <= 90.0)) {
				return 'l';
			}
			else {
				return 'g';
			}
		}
		else if ((psi216 >= 10.0) && (psi216 < 30.0)) {
			if ((phi216 >= 60.0) && (phi216 <= 90.0)) {
				return 'l';
			}
			else {
				return 'g';
			}
		}
		else if ((psi216 >= 30.0) && (psi216 < 40.0)) {
			if ((phi216 >= 50.0) && (phi216 <= 90.0)) {
				return 'l';
			}
			else {
				return 'g';
			}
		}
		else if ((psi216 >= 40.0) && (psi216 < 60.0)) {
			if ((phi216 >= 40.0) && (phi216 <= 80.0)) {
				return 'l';
			}
			else {
				return 'g';
			}
		}
		else if ((psi216 >= 60.0) && (psi216 < 70.0)) {
			if ((phi216 >= 40.0) && (phi216 <= 70.0)) {
				return 'l';
			}
			else {
				return 'g';
			}
		}
		else if ((psi216 >= 70.0) && (psi216 < 80.0)) {
			if ((phi216 >= 50.0) && (phi216 <= 60.0)) {
				return 'l';
			}
			else {
				return 'g';
			}
		}
		else {
			return 'g';
		}
	}
	else if (phi216 >= 135.0) {
		return 'b';
	}
	else {
		return 'e';
	}
}

/* phi216 < 0 psi216 >0  */

else if ((phi216 < 0.0) && (psi216 > 0.0)) {
	if (psi216 <= 45.0) {
		return 'a';
	}
	else if (psi216 <= 95.0) {
		return 't';
	}
	else if ((psi216 > 95.0) && (phi216 <= -125.0)) {
		calpsi = 4.0909 - ((40.0 * phi216)/55.0);
		if (psi216 <= calpsi) {
			return 't';
		}
		else {
			return 'b';
		}
	}
	else if ((psi216 > 95.0) && (phi216 > -125.0) && (phi216 <= -105.0)) {
		return 'b';
	}
	else if ((psi216 > 95.0) && (phi216 > -105.0)) {
		calpsi = -117.5 - ((85.0 * phi216)/30.0);
		if (psi216 <= calpsi) {
			return 'b';
		}
		else {
			return 'p';
		}
	}
}

/* phi216 < 0 psi216 <= 0  */

else if ((phi216 < 0.0) && (psi216 <= 0.0)) {
	calpsi = -125.0 - ((40.0 * phi216)/180.0);
	if (psi216 >= calpsi) {
		return 'a';
	}
	else if (phi216 <= -105.0) {
		return 'b';
	}
	else {
		return 'p';
	}
}

/* phi216 > 0 psi216 <= 0  */

else if ((phi216 > 0.0) && (psi216 <= 0.0)) {
	calpsi = -55.0 - ((80.0 * phi216)/180.0);
	if (psi216 >= calpsi) {
		return 'g';
	}
	else if (phi216 >= 135.0) {
		return 'b';
	}
	else {
		return 'e';
	}
}

/* undefined */

else {
	return 'X';
}
}

feature "solvent accessibility" {
if (PSA.missing_atom) {
	return '1';
#
# There is a but here in the old joy 216
#
# this has to be return '3'
}
else if (PSA.side_per <= 7.0) {
	return '1';
}
else if (PSA.side_per <= 40.0) {
	return '2';
}
else {
	return '3';
}
}

feature "hydrogen bond to mainchain CO" {
	return HBD.CO;
}

feature "hydrogen bond to mainchain NH" {
	return HBD.NH;
}

feature "hydrogen bond to other sidechain/heterogen" {
if (HBD.side eq 'T' || HBD.side_hetero eq 'T') {
	return 'T';
}
else {
	return 'F';
}
}

feature "cis-peptide bond" {  # This is a special case
                              # To assign the state for kth residue
                              # omega for (k-1)th residue has to be examined!

if (k > 0 && SST.omega[k-1] > -90.0 && SST.omega[k-1] < 90.0) {
	return 'T';
}
else {
	return 'F';
}
}

feature "hydrogen bond to heterogen" {
	return HBD.side_hetero;
}

feature "disulphide" {
	return HBD.disulphide;
}

feature "mainchain to mainchain hydrogen bonds (amide)" {
	return HBD.main_mainN;
}

feature "Mainchain to mainchain hydrogen bonds (carbonyl)" {
	return HBD.main_mainO;
}

feature "DSSP" {
if (SST.dssp eq 'H') {
	return 'H';
}
else if (SST.dssp eq 'G') {
	return 'G';
}
else if (SST.dssp eq 'I') {
	return 'I';
}
else if (SST.dssp eq 'E') {
	return 'E';
}
else {
	return 'C';
}
}

feature "positive phi angle" {
if ((SST.phi >= 0.0) && (SST.phi < 180.0)) {
	return 'T';
}
else {
	return 'F';
}
}

feature "percentage accessibility" {
	return realhex(PSA.side_per);
}

feature "Ooi number" {
	return inthex(SST.ooi);
}
