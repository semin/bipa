#
# joyfeature.ext
#
# A new extended set of structural features
# 1. hydrogen bond to mainchain (merging NH and CO)
# 2. hydrogen bond (mering all)
# 3. secondary structure (mering positive phi and coil)
#
# Type
# % mkjoyf joyfeature.ext
#
# to generate tem_default.c and tem_default.h.
#
# 10 Oct 2000

#generic rules {
#if (ALI.sequence eq ' ' || ALI.sequence eq '-' || ALI.sequence eq '/') {
#	return '-';
#}
#}

feature "secondary structure and phi angle" {
if ((SST.phi >= 0.0) && (SST.phi < 180.0)) {
	return 'P';
}
else if ((SST.dssp eq 'H') || (SST.dssp eq 'G') || (SST.dssp eq 'I')) { # all type of helices
	return 'H';
}
else if (SST.dssp eq 'E') {
	return 'E';
}
else {
	return 'C';
}
}

feature "solvent accessibility" {
if (PSA.missing_atom) {
	return 'T';
}
else if (PSA.side_per > psacutoff) {
	return 'T';
}
else {
	return 'F';
}
}

feature "hydrogen bond to mainchain CO" {
	return HBD.CO;
}

feature "hydrogen bond to mainchain NH" {
	return HBD.NH;
}

feature "hydrogen bond to other sidechain/heterogen" {
if (HBD.side eq 'T' || HBD.side_hetero eq 'T') {
	return 'T';
}
else {
	return 'F';
}
}

feature "cis-peptide bond" {  # This is a special case
                              # To assign the state for kth residue
                              # omega for (k-1)th residue has to be examined!

if (k > 0 && SST.omega[k-1] > -90.0 && SST.omega[k-1] < 90.0) {
	return 'T';
}
else {
	return 'F';
}
}

feature "hydrogen bond to heterogen" {
	return HBD.side_hetero;
}

feature "covalent bond to heterogen" {
	return HBD.cov_hetero;
}

feature "disulphide" {
	return HBD.disulphide;
}

feature "mainchain to mainchain hydrogen bonds (amide)" {
	return HBD.main_mainN;
}

feature "Mainchain to mainchain hydrogen bonds (carbonyl)" {
	return HBD.main_mainO;
}

feature "DSSP" {
if (SST.dssp eq 'H') {
	return 'H';
}
else if (SST.dssp eq 'G') {
	return 'G';
}
else if (SST.dssp eq 'I') {
	return 'I';
}
else if (SST.dssp eq 'E') {
	return 'E';
}
else {
	return 'C';
}
}

feature "positive phi angle" {
if ((SST.phi >= 0.0) && (SST.phi < 180.0)) {
	return 'T';
}
else {
	return 'F';
}
}

feature "percentage accessibility" {
	return realhex(PSA.side_per);
}

feature "Ooi number" {
	return inthex(SST.ooi);
}

feature "hydrogen bond to mainchain" {
if (HBD.NH eq 'T' || HBD.CO eq 'T') {
	return 'T';
}
else {
	return 'F';
}
}

feature "hydrogen bond" {
if (HBD.NH eq 'T' || HBD.CO eq 'T' || HBD.side eq 'T' || HBD.side_hetero eq 'T') {
	return 'T';
}
else {
	return 'F';
}
}

feature "secondary structure" {
if ((SST.dssp eq 'H') || (SST.dssp eq 'G') || (SST.dssp eq 'I')) { # all type of helices
	return 'H';
}
else if (SST.dssp eq 'E') {
	return 'E';
}
else {
	return 'C';
}
}
